(ns club.bhikers.lib.app
  (:require ["package:flutter_settings_screens/flutter_settings_screens.dart" :refer [Settings]]
            ["package:shared_preferences/shared_preferences.dart" :refer [SharedPreferences]]
            ["package:flutter/material.dart" :as m]
            ["package:package_info_plus/package_info_plus.dart" :refer [PackageInfo]]
            ["package:flutter_settings_screens/flutter_settings_screens.dart" :as settings]
            ["package:easy_localization/easy_localization.dart" :as l10n]
            [club.bhikers.lib.config :as config]
            [club.bhikers.lib.utils :refer [str->location-accuracy]]))

(defn init! [& {:keys [mock-mode?] :or {mock-mode? false}}]
  (when mock-mode? (-> SharedPreferences (.setMockInitialValues {})))

  (m/WidgetsFlutterBinding.ensureInitialized)
  (settings/Settings.init .cacheProvider (settings/SharePreferenceCache))
  (await (l10n/EasyLocalization.ensureInitialized))

  (let [pkg-info (if mock-mode?
                   (PackageInfo .appName "test" .packageName "test" .version "1.0.0" .buildNumber "42" .buildSignature "")
                   (await (.fromPlatform PackageInfo)))
        _duration-setting (#/(.getValue int) Settings "/advanced-location/refresh-location-interval-duration"
                             .defaultValue 30)
        _accuracy-setting (#/(.getValue String) Settings "/advanced-location/refresh-location-accuracy"
                             .defaultValue "low")
        _distance-setting (#/(.getValue String) Settings "/advanced-location/refresh-location-distance-filter"
                             .defaultValue "500")
        _radius-setting (#/(.getValue double) Settings "/general/pois-radius" .defaultValue 1000)
        _query-timeout-setting (#/(.getValue String) Settings "/advanced/api-query-timeout" .defaultValue "5")
        _debug-setting (#/(.getValue bool) Settings "/advanced/debug-mode?" .defaultValue false)
        _poi-type-setting (#/(.getValue String) Settings "/general/default-poi-type" .defaultValue config/default-poi-type)

        ;; Map Init Logic: determine which server key to read based on layer type
        _tile-layer-type-setting (#/(.getValue String) Settings "/general/tile-layer-type" .defaultValue "raster")

        ;; Dynamic key selection:
        server-key (if (= _tile-layer-type-setting "raster")
                       "/general/tile-server/raster"
                       "/general/tile-server/vector")
        default-server (if (= _tile-layer-type-setting "raster") "osm" "protomaps")

        _tile-server-setting (#/(.getValue String) Settings server-key .defaultValue default-server)]

    (reset! config/app-info {:build-name (.-version pkg-info)
                             :build-number (.-buildNumber pkg-info)
                             :app-name (.-appName pkg-info)
                             :git-commit (String.fromEnvironment "GIT_COMMIT" .defaultValue "undef")
                             :git-branch (String.fromEnvironment "GIT_BRANCH" .defaultValue "undef")
                             :repo-url  (String.fromEnvironment "REPO_URL" .defaultValue "undef")
                             :latest-release-api  (String.fromEnvironment "LATEST_RELEASE_API"
                                                                          .defaultValue config/default-latest-release-api)})
    (reset! config/refresh-location-distance-filter (int/tryParse _distance-setting))
    (reset! config/refresh-location-interval-duration (Duration .seconds _duration-setting))
    (reset! config/refresh-location-accuracy (str->location-accuracy _accuracy-setting))
    (reset! config/pois-radius _radius-setting)
    (reset! config/api-query-timeout (int/tryParse _query-timeout-setting))
    (reset! config/force-debug-mode? _debug-setting)
    (reset! config/selected-poi-type _poi-type-setting)
    (reset! config/selected-tile-server _tile-server-setting)
    (reset! config/tile-layer-type (keyword _tile-layer-type-setting))))