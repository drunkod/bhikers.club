(ns club.bhikers.screens.common.appbar
  (:require ["package:flutter/material.dart" :as m]
            [club.bhikers.lib.utils :refer [l10n-str]]
            [club.bhikers.lib.notifications
             :refer [dismiss-notif dismiss-all-notifs notifications]]
            [club.bhikers.screens.debuglog :refer [debuglog-screen]]
            [cljd.flutter :as f]))

;; =============================================================================
;; Notifications Widgets
;; =============================================================================

(defn notifications-list-widget []
  (f/widget
   :get [:notifications]
   :watch [*notifs notifications]
   (m/ListView
    .padding (m/EdgeInsets.all 2.0)
    .children
    (into [(m/ListTile
            .leading (m/Icon m/Icons.notification_important)
            .title (m/Text (l10n-str "notifications.title"))
            .trailing (m/IconButton
                       .icon (m/Icon m/Icons.delete_sweep)
                       .tooltip (l10n-str "notifications.dismiss_all")
                       .onPressed #(dismiss-all-notifs)))]
          (map
           (fn [[id txt]] (m/Dismissible
                           .key (m/ValueKey id)
                           .onDismissed (fn [_] (dismiss-notif id))
                           .child (m/ListTile .title (m/Text txt))))
           *notifs)))))

(defn notifications-button []
  (f/widget
   :context ctx
   :get [:notifications]
   :watch [*notifs notifications]
   (m/Stack .children
            (let [nb-notifs (count *notifs)]
              (if (> 1 nb-notifs)
                []
                [(m/IconButton .icon (m/Icon m/Icons.notification_important)
                               .tooltip "important"
                               .onPressed (fn []
                                            (m/showModalBottomSheet
                                             .context ctx
                                             .builder (f/build
                                                       (f/widget
                                                        (m/Container .height 300)
                                                        (notifications-list-widget))))
                                            nil))
                 (f/widget
                  (m/Positioned .right 12 .top 12)
                  (m/Container  .padding (m/EdgeInsets.all 5)
                                .decoration (m/BoxDecoration
                                             .shape m/BoxShape.circle
                                             .color m/Colors.red)
                                .constraints (m/BoxConstraints
                                              .minWidth 5
                                              .minHeight 5)))])))))

;; =============================================================================
;; Debug Log Button
;; =============================================================================

(defn debuglog-screen-button []
  (f/widget :get [m/Navigator]
            (m/IconButton .icon (m/Icon m/Icons.article)
                          .tooltip "logs"
                          .onPressed (fn []
                                       (.push navigator
                                              (m/MaterialPageRoute
                                               .builder (f/build [] (debuglog-screen))))
                                       nil))))

;; =============================================================================
;; Shared App Bar
;; =============================================================================

(defn app-bar [& {:keys [title
                         additional-actions]
                  :or {title "app.title"
                       additional-actions []}}]
  (m/AppBar .title (m/Text (l10n-str title))
            .actions (concat
                      [(debuglog-screen-button)
                       (notifications-button)]
                      additional-actions)))
