(ns club.bhikers.screens.common.drawer
  (:require ["package:flutter/material.dart" :as m]
            ["package:url_launcher/link.dart" :as link]
            [club.bhikers.lib.utils :refer [l10n-str]]
            [club.bhikers.lib.config :refer [feat-enhance-gpx?
                                          feat-fall-detector?
                                          app-info
                                          new-version
                                          new-version-url]]
            [cljd.flutter :as f]))

;; =============================================================================
;; Main Navigation Drawer
;; =============================================================================

(defn drawer []
  (f/widget
   :get [m/Navigator
         m/Theme]
   :watch [*new-version-url new-version-url
           *new-version new-version]
   :let [drawer-routes (concat [["/around-me"
                                 (l10n-str "around_me.screen_title")
                                 m/Icons.location_pin]]
                               (if (not feat-enhance-gpx?) []
                                   [["/enhance-gpx"
                                     (l10n-str "enhance_gpx.screen_title")
                                     m/Icons.hotel_class_outlined]])
                               (if (not feat-fall-detector?) []
                                   [["/fall-detector"
                                     (l10n-str "fall_detector.screen_title")
                                     m/Icons.crisis_alert]])
                               [["/settings"
                                 (l10n-str "settings.screen_title")
                                 m/Icons.settings]])]
   m/Drawer
   (m/ListView
    .padding (m/EdgeInsets.all 2.0)
    .children (concat
               [(m/UserAccountsDrawerHeader
                 .accountName (m/Text "Anonymous")
                 .accountEmail (m/Text "foo@bar.com")
                 .currentAccountPicture (m/CircleAvatar .child (m/Text "JD")))]
               (map (fn [[route title icon]]
                      (m/ListTile
                       .leading (m/Icon icon)
                       .title (m/Text title)
                       .onTap
                       (fn []
                         (.pop navigator)
                         (.pushNamed navigator route)
                         nil)))
                    drawer-routes)
               [(m/AboutListTile
                 .icon (m/Icon m/Icons.info)
                 .applicationIcon nil
                 .applicationName (get @app-info :app-name)
                 .applicationVersion (str (get @app-info :build-name) "+" (get @app-info :build-number)
                                          " (" (get @app-info :git-commit) ")")
                 .applicationLegalese (l10n-str "about.legalese")
                 .aboutBoxChildren (concat
                                    [(m/SizedBox .height 24)
                                     (m/RichText .text (m/TextSpan .style (-> theme .-textTheme .-bodyMedium)
                                                                   .text (l10n-str "about.description")))]))]
               (if (not *new-version-url)
                 []
                 [(m/ListTile
                       .leading (m/Icon m/Icons.warning)
                       .title (m/Text (str (l10n-str "about.new_version") ": "*new-version "!")))
                  (link/Link .uri (Uri.parse *new-version-url)
                             .target link/LinkTarget.blank
                             .builder (f/build [open-link]
                                               (f/widget
                                                (m/TextButton.icon .onPressed open-link
                                                                   .label (m/Text *new-version-url .overflow m/TextOverflow.fade .softWrap true .maxLines 1)
                                                                   .icon (m/Icon m/Icons.system_update)))))])))))
